# Execution begins at address 0
    .pos 0
    irmovq stack,%rsp       # Set up stack pointer
    call main               # Execute main program
    halt                    # Terminate program

# Sample linked list
    .align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

main:
    irmovq ele1,%rdi        # Push head of linked-list
    call sum_list           # sum_list(ls)
    ret

# long sum_list(list_ptr ls)
sum_list:
    rrmovq %rdi,%rsi        # %rsi = ls
    xorq %rax,%rax          # sum = 0
    andq %rsi,%rsi          # Set CC
    jmp test                # Goto test
loop:
    mrmovq (%rsi),%r8
    addq %r8,%rax           # Accumulate
    mrmovq 8(%rsi),%rsi
    andq %rsi,%rsi          # Set CC
test:
    jne loop                # Stop when 0
    ret

# Stack starts here and grows to lower addresses
    .pos 0x200
stack:
